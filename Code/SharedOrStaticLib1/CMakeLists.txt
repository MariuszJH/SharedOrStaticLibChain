#[==================================================[
SharedOrStaticLib1

If BUILD_SHARED_LIBS is ON, SHARED library will be built
If BUILD_SHARED_LIBS is Off, STATIC library will be built
#]==================================================]
if(BUILD_SHARED_LIBS)
    set(isThereDllToLinkToDriver ON CACHE BOOL "overwrite default or previous option" FORCE)
    set(sharedOrStaticType shared)
    message(STATUS "BUILD_SHARED_LIBS: " ${BUILD_SHARED_LIBS} " --- targetSharedOrStaticLib1 will be built as SHARED library")
else()
    set(isThereDllToLinkToDriver OFF CACHE BOOL "overwrite default or previous option" FORCE)
    set(sharedOrStaticType static)
    message(STATUS "BUILD_SHARED_LIBS: " ${BUILD_SHARED_LIBS} " --- targetSharedOrStaticLib1 will be built as STATIC library")
endif()

message("#1 isThereDllToLinkToDriver: " ${isThereDllToLinkToDriver})

add_library(targetSharedOrStaticLib1)

set(CMAKE_POSITION_INDEPENDENT_CODE ON)

target_include_directories(targetSharedOrStaticLib1
    PRIVATE
        hdr
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

#[=========================[
N.B. 
Public headers are:
-- header files which need to be explicitly included by another application/library
-- header files which are included in the aforementioned header files.
Private header are:
-- header files which are only included in source (*.cpp, *.c) files but not in any of the public header files
-- header files included in other private header files
]=========================]

target_sources(targetSharedOrStaticLib1
    PRIVATE
        hdr/SharedOrStaticLib1.hpp
        src/SharedOrStaticLib1.cpp
        src/SharedOrStaticLib1Api.cpp
    PUBLIC 
        FILE_SET HEADERS 
        BASE_DIRS ${CMAKE_CURRENT_SOURCE_DIR}/include
        # BASE_DIRS ${CMAKE_SOURCE_DIR}/Code/SharedOrStaticLib1/include/
        FILES 
            ${CMAKE_CURRENT_SOURCE_DIR}/include/SharedOrStaticLib1Api.hpp
            # ${CMAKE_SOURCE_DIR}/Code/SharedOrStaticLib1/include/SharedOrStaticLib1Api.hpp
)

set_target_properties(targetSharedOrStaticLib1
    PROPERTIES
        OUTPUT_NAME                         SharedOrStaticLib1_${sharedOrStaticType}
        EXPORT_NAME                         SharedOrStaticLib1
        DEBUG_POSTFIX                       "_d"
        RUNTIME_OUTPUT_DIRECTORY            ${CMAKE_BINARY_DIR}/buildOut/SharedOrStaticLib1/bin/$<CONFIG>
        PDB_OUTPUT_DIRECTORY                ${CMAKE_BINARY_DIR}/buildOut/SharedOrStaticLib1/pdb/$<CONFIG>
        LIBRARY_OUTPUT_DIRECTORY            ${CMAKE_BINARY_DIR}/buildOut/SharedOrStaticLib1/lib/$<CONFIG>
        ARCHIVE_OUTPUT_DIRECTORY            ${CMAKE_BINARY_DIR}/buildOut/SharedOrStaticLib1/lib/$<CONFIG>
        RUNTIME_OUTPUT_DIRECTORY_<Config>   ${RUNTIME_OUTPUT_DIRECTORY}
        PDB_OUTPUT_DIRECTORY_<Config>       ${PDB_OUTPUT_DIRECTORY}
        LIBRARY_OUTPUT_DIRECTORY_<Config>   ${LIBRARY_OUTPUT_DIRECTORY}
        ARCHIVE_OUTPUT_DIRECTORY_<Config>   ${ARCHIVE_OUTPUT_DIRECTORY}
)

target_compile_features(targetSharedOrStaticLib1
    PUBLIC
        cxx_std_20
)

if(MSVC)

    target_compile_options(targetSharedOrStaticLib1 
        PRIVATE
            /EHsc
            /W2
            # /Wall
    )

else()

    target_compile_options(targetSharedOrStaticLib1 
        PRIVATE
            -Wall
            # -Wextra
            # -Wpedantic
    )

endif()

#[==================================================[
Let cmake generate proper __declspec(dllexport) and __declspec(dllexport) needed by Windows
#]==================================================]
include(GenerateExportHeader)
generate_export_header(targetSharedOrStaticLib1
    EXPORT_FILE_NAME ${CMAKE_CURRENT_SOURCE_DIR}/include/AutoGeneratedSharedOrStaticLib1Exports.hpp
    # EXPORT_FILE_NAME ${CMAKE_SOURCE_DIR}/Code/SharedOrStaticLib1/include/AutoGeneratedSharedOrStaticLib1Exports.hpp
    EXPORT_MACRO_NAME SHARED_OR_STATIC_LIB1_API
    INCLUDE_GUARD_NAME AUTO_GENERATED_SHARED_OR_STATIC_LIB1_EXPORTS_HPP
)

# target_compile_definitions(targetSharedOrStaticLib1 
#     PRIVATE 
#         $<$<NOT:$<BOOL:${BUILD_SHARED_LIBS}>>:targetSharedOrStaticLib1_EXPORTS>
#         # $<$<NOT:$<BOOL:${BUILD_SHARED_LIBS}>>:targetSharedOrStaticLib1_STATIC_DEFINE>
# )

#[==================================================[
Link executable with libraries
#]==================================================]
target_link_libraries(targetSharedOrStaticLib1
    PRIVATE 
        targetSharedOrStaticLib2
)
